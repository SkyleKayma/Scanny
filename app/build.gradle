plugins {
    id "com.android.application"
    id "com.google.gms.google-services"
    id "kotlin-android"
    id "kotlin-kapt"
    id "com.google.firebase.crashlytics"
    id "com.google.firebase.appdistribution"
    id "com.starter.easylauncher"
    id "dagger.hilt.android.plugin"
}

def keystorePropertiesFile = rootProject.file("private/keys/keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdk 33

    defaultConfig {
        applicationId "fr.skyle.scanny"
        minSdk 23
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        ndk { abiFilters "arm64-v8a", "armeabi-v7a", "x86_64" }
    }

    signingConfigs {
        debug {
            storeFile file(keystoreProperties["debugStoreFile"])
        }
        release {
            storeFile file(keystoreProperties["releaseStoreFile"])
            storePassword keystoreProperties["passwordRelease"]
            keyAlias keystoreProperties["aliasRelease"]
            keyPassword keystoreProperties["passwordRelease"]
        }
    }

    buildTypes {
        debug {
            minifyEnabled false

            versionNameSuffix "-debug"
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.debug

            firebaseAppDistribution {
                artifactType = "AAB"
                groups = "Developper"
                serviceCredentialsFile = file("../private/serviceCredentials/firebase.json")
            }
        }
        release {
            minifyEnabled false

            signingConfig signingConfigs.release

            firebaseAppDistribution {
                artifactType = "AAB"
                groups = "Developper"
                serviceCredentialsFile = file("../private/serviceCredentials/firebase.json")
            }
        }
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion "1.3.0"
    }

    packagingOptions {
        resources {
            excludes += [
                    "META-INF/DEPENDENCIES",
                    "META-INF/DEPENDENCIES.txt",
                    "META-INF/NOTICE",
                    "META-INF/NOTICE.txt",
                    "META-INF/LICENSE",
                    "META-INF/LICENSE.txt",
                    "META-INF/{AL2.0,LGPL2.1}"
            ]
        }
    }
}

easylauncher {
    buildTypes {
        debug {
            filters greenRibbonFilter("DEBUG")
        }
    }
}

task clonePrivateFiles {
    doLast {
        copy {
            from '../private/firebase/google-services.json'
            into './'
        }
    }
}

// This is needed to avoid duplicate in demo targets
project.afterEvaluate {
    getTasks().getByName("preBuild").finalizedBy(clonePrivateFiles)
}

dependencies {
    // Project
//    implementation project(":data")

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // AndroidX
    implementation "androidx.core:core-ktx:1.8.0"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.5.1"
    implementation "androidx.activity:activity-compose:1.5.1"

    // Lifecycle
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"

    // Timber
    implementation "com.jakewharton.timber:timber:5.0.1"

    // Compose
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation "androidx.navigation:navigation-compose:2.5.1"
    implementation "androidx.compose.runtime:runtime-livedata:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"

    // Accompanist
    implementation "com.google.accompanist:accompanist-pager:0.25.1"
    implementation "com.google.accompanist:accompanist-pager-indicators:0.25.1"
    implementation "com.google.accompanist:accompanist-permissions:0.25.1"
    implementation "com.google.accompanist:accompanist-insets:0.25.1"
    implementation "com.google.accompanist:accompanist-insets-ui:0.25.1"
    implementation "com.google.accompanist:accompanist-navigation-animation:0.25.1"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutine_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutine_version"

    // Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"
    kapt "androidx.hilt:hilt-compiler:1.0.0"
    implementation "androidx.hilt:hilt-navigation-fragment:1.0.0"
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0"

    // Firebase
    implementation platform("com.google.firebase:firebase-bom:29.0.4")
    implementation "com.google.firebase:firebase-crashlytics-ktx"

    // ML Kit
    implementation "com.google.mlkit:barcode-scanning:17.0.2"

    // Camera 2
    implementation "androidx.camera:camera-camera2:1.1.0"
    implementation "androidx.camera:camera-lifecycle:1.1.0"
    implementation "androidx.camera:camera-core:1.1.0"
    implementation "androidx.camera:camera-extensions:1.1.0"
    implementation "androidx.camera:camera-view:1.1.0"

    // Coil
    implementation "io.coil-kt:coil-base:1.4.0"

    // Datastore
    implementation "androidx.datastore:datastore:1.0.0"
    implementation "androidx.datastore:datastore-preferences:1.0.0"

    // Tests
    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test.ext:junit:1.1.3"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
}