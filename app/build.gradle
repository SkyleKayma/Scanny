plugins {
    id "com.android.application"
    id "com.google.gms.google-services"
    id "kotlin-android"
    id "kotlin-parcelize"
    id "kotlin-kapt"
    id "com.google.firebase.crashlytics"
    id "com.google.firebase.appdistribution"
    id "com.starter.easylauncher"
    id "dagger.hilt.android.plugin"
}

// Keystore
def keystorePropertiesFile = rootProject.file("private/keys/keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

// Version
def versionPropertiesFile = rootProject.file("app/version.properties")
def versionProperties = new Properties()
versionProperties.load(new FileInputStream(versionPropertiesFile))

def code = versionProperties["versionCode"].toInteger()

android {
    namespace "fr.skyle.scanny"

    compileSdk 33

    defaultConfig {
        applicationId "fr.skyle.scanny"
        minSdk 23
        targetSdk 33
        versionCode code
        versionName versionProperties["versionName"]

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        ndk { abiFilters "arm64-v8a", "armeabi-v7a", "x86_64" }
        resConfigs "en", "fr"
    }

    signingConfigs {
        debug {
            storeFile file(keystoreProperties["debugStoreFile"])
        }
        release {
            storeFile file(keystoreProperties["releaseStoreFile"])
            storePassword keystoreProperties["passwordRelease"]
            keyAlias keystoreProperties["aliasRelease"]
            keyPassword keystoreProperties["passwordRelease"]
        }
    }

    buildTypes {
        debug {
            minifyEnabled false

            versionNameSuffix "-debug"
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.debug

            firebaseAppDistribution {
                artifactType = "APK"
                groups = "Developper"
                serviceCredentialsFile = file("./google-services-account.json")
            }
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"

            signingConfig signingConfigs.release

            firebaseAppDistribution {
                artifactType = "APK"
                groups = "Developper"
                serviceCredentialsFile = file("./google-services-account.json")
            }
        }
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion "1.3.2"
    }

    packagingOptions {
        resources {
            excludes += [
                    "META-INF/DEPENDENCIES",
                    "META-INF/DEPENDENCIES.txt",
                    "META-INF/NOTICE",
                    "META-INF/NOTICE.txt",
                    "META-INF/LICENSE",
                    "META-INF/LICENSE.txt",
                    "META-INF/{AL2.0,LGPL2.1}"
            ]
        }
    }

    lintOptions {
        disable "RestrictedApi", "ParcelCreator"
    }
}

easylauncher {
    buildTypes {
        debug {
            filters greenRibbonFilter("DEBUG")
        }
    }
}

task clonePrivateFiles {
    notCompatibleWithConfigurationCache("custom")
    doLast {
        copy {
            from '../private/firebase/google-services.json'
            into './'
        }
    }
}

task incrementBuildNumberAfterPublish {
    doLast {
        print("Incrementing build number by 1 (current $code)")
        code += 1
        versionProperties["versionCode"] = code.toString()
        versionProperties.store(versionPropertiesFile.newWriter(), null)
    }
}

// This is needed to avoid duplicate in demo targets
project.afterEvaluate {
    getTasks().getByName("preBuild").finalizedBy(clonePrivateFiles)
}

dependencies {
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // AndroidX
    implementation "androidx.core:core-ktx:1.9.0"
    implementation "androidx.activity:activity-compose:1.6.1"

    // Lifecycle
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"

    // Timber
    implementation "com.jakewharton.timber:timber:5.0.1"

    // Compose
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation "androidx.navigation:navigation-compose:2.5.3"
    implementation "androidx.compose.runtime:runtime-livedata:$compose_version"
    implementation "androidx.compose.animation:animation-graphics:$compose_version"
    implementation "androidx.constraintlayout:constraintlayout-compose:1.1.0-alpha05"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"

    // Accompanist
    implementation "com.google.accompanist:accompanist-pager:$accompanist_version"
    implementation "com.google.accompanist:accompanist-pager-indicators:$accompanist_version"
    implementation "com.google.accompanist:accompanist-permissions:$accompanist_version"
    implementation "com.google.accompanist:accompanist-navigation-animation:$accompanist_version"
    implementation "com.google.accompanist:accompanist-systemuicontroller:$accompanist_version"
    implementation "com.google.accompanist:accompanist-flowlayout:$accompanist_version"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutine_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutine_version"

    // Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"
    kapt "androidx.hilt:hilt-compiler:1.0.0"
    implementation "androidx.hilt:hilt-navigation-fragment:1.0.0"
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0"

    // Firebase
    implementation platform("com.google.firebase:firebase-bom:30.4.1")
    implementation "com.google.firebase:firebase-crashlytics-ktx"
    implementation "com.google.firebase:firebase-analytics-ktx"

    // ML Kit
    implementation "com.google.mlkit:barcode-scanning:17.0.3"

    // QRCode generation
    implementation "io.github.g0dkar:qrcode-kotlin-android:3.2.0"

    // Camera 2
    implementation "androidx.camera:camera-camera2:$camera2_version"
    implementation "androidx.camera:camera-lifecycle:$camera2_version"
    implementation "androidx.camera:camera-core:$camera2_version"
    implementation "androidx.camera:camera-extensions:$camera2_version"
    implementation "androidx.camera:camera-view:$camera2_version"

    // Coil
    implementation "io.coil-kt:coil-compose-base:2.2.2"

    // Datastore
    implementation "androidx.datastore:datastore:1.0.0"
    implementation "androidx.datastore:datastore-preferences:1.0.0"

    // Tests
    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test.ext:junit-ktx:1.1.5"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.5.1"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
}